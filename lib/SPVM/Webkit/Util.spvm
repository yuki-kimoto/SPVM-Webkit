class Webkit::Util {
  use Regex;
  use Format;

  static method url_escape : string ($input : string) {
    
    # URL escape
    my $regex = Regex->new("([^A-Za-z0-9\-\._~])");
    my $output = $regex->replace_g_cb($input, method : string ($re : Regex) {
      my $bytes = $re->cap1;
      
      my $escaped_char_buffer = StringBuffer->new;
      
      for (my $i = 0; $i < length $bytes; $i++) {
        my $byte = $bytes->[$i];
        my $hex_string = Format->sprintf("%%%02X", [(object)($byte & 0xFF)]);
        $escaped_char_buffer->push($hex_string);
      }
      
      my $escaped_char = $escaped_char_buffer->to_string;
      
      return $escaped_char;
    });
    
    return $output;
  }
  
  static method url_unescape : string ($input : string) {
    
    # URL unescape
    my $regex = Regex->new("%([0-9a-fA-F]{2,2})");
    my $output = $regex->replace_g_cb($input, method : string ($re : Regex) {
      my $cap1 = $re->cap1;
      my $hex = Fn->hex($cap1);
      my $ch = (string)[(byte)$hex];
      return $ch;
    });
    
    return $output;
  }
}
